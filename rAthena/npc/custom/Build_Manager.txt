prontera,150,193,4	script	Reset Girl	124,{
function ManageBuild;

mes "Select Your Services";
next;
switch( select( ( .BMMode & 1 )?"^FF0000Save & Switch Builds^000000":"",
				( .BMMode & 2 )?"^0000FFReset Status^000000":"",	
				( .BMMode & 4 )?"^0000FFReset Skills^000000":"")){
	Case 1:
		for( set .@i,1; .@i <= .MaxSlots; set .@i,.@i + 1 )
			set .@Menu$,.@Menu$ + (( getd( "Slot_"+.@i+"$" ) == "" )?"^FF0000Empty Slot^000000":getd( "Slot_"+.@i+"$" ) )+":";
		set .@Slot,select( .@Menu$ );
		do{
			next;
			mes "Build : ^FF0000"+(( getd( "Slot_"+.@Slot+"$" ) == "" )?"Slot "+.@Slot:getd( "Slot_"+.@Slot+"$" ) )+"^000000";
			mes "STR - ^0000FF"+getd( "STR_"+.@Slot )+"^000000      INT - ^0000FF"+getd( "INT_"+.@Slot )+"^000000";
			mes "AGI - ^0000FF"+getd( "AGI_"+.@Slot )+"^000000      DEX - ^0000FF"+getd( "DEX_"+.@Slot )+"^000000";
			mes "VIT - ^0000FF"+getd( "VIT_"+.@Slot )+"^000000      LUK - ^0000FF"+getd( "LUK_"+.@Slot )+"^000000";
			mes " ";
			mes "Remaining Points : ^0000FF"+getd( "STATS_"+.@Slot )+"^000000";
			set .@Option,select(( getd( "Slot_"+.@Slot+"$" ) == "" )?"":"^0000FFSwitch",
								( getd( "Slot_"+.@Slot+"$" ) == "" )?"Save":"",
								( getd( "Slot_"+.@Slot+"$" ) == "" )?"":"^FF0000Clear" );
			ManageBuild( .@Slot,.@Option);
		}while( .@Option != 1 );
		break;
	Case 2:
		ResetStatus;
		mes "Stat points have been reset.";
		break;
	Case 3:
		ResetSkill;
		mes "Skills have been reset.";
		break;
}
close;

function	ManageBuild	{
switch( getarg(1) ){
	Case 1:	//	Switch Build
		message strcharinfo(0),"Build has been ^FF0000Switched^000000.";
			ResetStatus;
			set StatusPoint,getd( "STATS_"+getarg(0) );
			statusup2 bStr,getd( "STR_"+getarg(0) ) - 1;
			statusup2 bAgi,getd( "AGI_"+getarg(0) ) - 1;
			statusup2 bVit,getd( "VIT_"+getarg(0) ) - 1;
			statusup2 bInt,getd( "INT_"+getarg(0) ) - 1;
			statusup2 bDex,getd( "DEX_"+getarg(0) ) - 1;
			statusup2 bLuk,getd( "LUK_"+getarg(0) ) - 1;
		break;
	Case 2:	//	Save Build
			setd( "STR_"+getarg(0) ),readparam(bStr);
			setd( "AGI_"+getarg(0) ),readparam(bAgi);
			setd( "VIT_"+getarg(0) ),readparam(bVIT);
			setd( "INT_"+getarg(0) ),readparam(bInt);
			setd( "DEX_"+getarg(0) ),readparam(bDex);
			setd( "LUK_"+getarg(0) ),readparam(bLuk);
			setd( "STATS_"+getarg(0) ),StatusPoint;
		if( .Length[0] < .Length[1] && .Length[0] ){
			mes "Input a ^FF0000Name^000000 for this Slot.";
			mes "Name Length : "+.Length[0]+" ~ "+.Length[1]+" Chars";
			do{
				input getd( "Slot_"+getarg(0)+"$" );
			}while( getstrlen( getd( "Slot_"+getarg(0)+"$" ) ) < .Length[0] || getstrlen( getd( "Slot_"+getarg(0)+"$" ) ) > .Length[1] );
		}else{
			setd( "Slot_"+getarg(0)+"$" ),"Build "+getarg(0);
		}
		message strcharinfo(0),"Build has been ^FF0000Saved^000000.";
		break;
	Case 3:	//	Remove Build
		message strcharinfo(0),"Build has been ^FF0000Cleared^000000.";
			setd( "STR_"+getarg(0) ),0;
			setd( "AGI_"+getarg(0) ),0;
			setd( "VIT_"+getarg(0) ),0;
			setd( "INT_"+getarg(0) ),0;
			setd( "DEX_"+getarg(0) ),0;
			setd( "LUK_"+getarg(0) ),0;
			setd( "STATS_"+getarg(0) ),0;
			setd( "Slot_"+getarg(0)+"$" ),"";
		break;
}
return;
}	
	
OnInit:
// Mode of NPC
// 1 = Save & Switch 
// 2 = Reset Status
// 4 = Reset Skills
// 7 = All of Above [ 1+2+4 = 7 ]
set .BMMode,7;
// How Many Slots available to Save Status Builds.
set .MaxSlots,3;
// Enable Stats Slot Rename + Max Name Length ( 0 = Disable )
setarray .Length,4,26; // Min. ~ Max. Words.
end;

}

// Duplicates
//============================================================
alberta,33,240,6	duplicate(Reset Girl)	Reset Girl#alb	124
aldebaran,133,117,6	duplicate(Reset Girl)	Reset Girl#alde	124
amatsu,203,85,4	duplicate(Reset Girl)	Reset Girl#ama	124
ayothaya,203,173,6	duplicate(Reset Girl)	Reset Girl#ayo	124
comodo,185,163,6	duplicate(Reset Girl)	Reset Girl#com	124
einbech,60,43,6	duplicate(Reset Girl)	Reset Girl#einbe	124
einbroch,72,205,6	duplicate(Reset Girl)	Reset Girl#einbr	124
geffen,124,69,6	duplicate(Reset Girl)	Reset Girl#gef	124
gonryun,156,126,6	duplicate(Reset Girl)	Reset Girl#gon	124
hugel,90,145,6	duplicate(Reset Girl)	Reset Girl#hug	124
izlude,121,150,5	duplicate(Reset Girl)	Reset Girl#izl	124
jawaii,248,141,4	duplicate(Reset Girl)	Reset Girl#jaw	124
lighthalzen,151,102,6	duplicate(Reset Girl)	Reset Girl#lhz	124
louyang,212,105,4	duplicate(Reset Girl)	Reset Girl#lou	124
manuk,270,142,6	duplicate(Reset Girl)	Reset Girl#man	124
mid_camp,202,286,6	duplicate(Reset Girl)	Reset Girl#mid	124
moc_ruins,72,164,4	duplicate(Reset Girl)	Reset Girl#moc	124
morocc,150,97,6	duplicate(Reset Girl)	Reset Girl#mor	124
moscovia,217,191,4	duplicate(Reset Girl)	Reset Girl#mos	124
niflheim,212,182,5	duplicate(Reset Girl)	Reset Girl#nif	124
payon,166,99,4	duplicate(Reset Girl)	Reset Girl#pay	124
rachel,122,117,6	duplicate(Reset Girl)	Reset Girl#rac	124
splendide,208,153,4	duplicate(Reset Girl)	Reset Girl#spl	124
thor_camp,249,74,4	duplicate(Reset Girl)	Reset Girl#thor	124
umbala,102,150,3	duplicate(Reset Girl)	Reset Girl#umb	124
veins,221,125,4	duplicate(Reset Girl)	Reset Girl#ve	124
xmas,140,128,4	duplicate(Reset Girl)	Reset Girl#xmas	124
yuno,151,45,4	duplicate(Reset Girl)	Reset Girl#yuno	124

// Duplicates (Renewal)
//============================================================
brasilis,189,226,6	duplicate(Reset Girl)	Reset Girl#bra	124
dewata,197,189,4	duplicate(Reset Girl)	Reset Girl#dew	124
dicastes01,188,197,4	duplicate(Reset Girl)	Reset Girl#dic	124
malangdo,142,110,6	duplicate(Reset Girl)	Reset Girl#mal	124
malaya,240,209,6	duplicate(Reset Girl)	Reset Girl#ma	124
mora,50,149,4	duplicate(Reset Girl)	Reset Girl#mora	124